#-------------------------------------------------------------------------------
# SPDX-FileCopyrightText: Copyright The TrustedFirmware-M Contributors
#
# SPDX-License-Identifier: BSD-3-Clause
#
#-------------------------------------------------------------------------------

if (NOT TFM_PARTITION_RSE_IMAGE_VERIFICATION)
    return()
endif()

cmake_minimum_required(VERSION 3.21)
cmake_policy(SET CMP0079 NEW)

# The name of the target is required to be of the pattern
# tfm_app_rot_partition_x or tfm_psa_rot_partition_x, as it affects how the
# linker script will lay the partition in memory.
add_library(tfm_app_rot_partition_rse_image_verification STATIC)

target_sources(tfm_app_rot_partition_rse_image_verification
    PRIVATE
        rse_image_verification_req_mngr.c
)

# Add the source files generated by parse tools when building.
# The intermedia file defines the partition stack.
target_sources(tfm_app_rot_partition_rse_image_verification
    PRIVATE
        ${CMAKE_BINARY_DIR}/generated/secure_fw/partitions/rse_image_verification/auto_generated/intermedia_tfm_rse_image_verification.c
)

# The load info file includes the static data of the partition.
target_sources(tfm_partitions
    INTERFACE
        ${CMAKE_BINARY_DIR}/generated/secure_fw/partitions/rse_image_verification/auto_generated/load_info_tfm_rse_image_verification.c
)

# Add dependency to the manifest_tool
add_dependencies(tfm_app_rot_partition_rse_image_verification manifest_tool)

target_include_directories(tfm_app_rot_partition_rse_image_verification
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}
    PUBLIC
        ${CMAKE_BINARY_DIR}/generated/secure_fw/partitions/rse_image_verification
)

target_link_libraries(tfm_app_rot_partition_rse_image_verification
    PRIVATE
        platform_s
        tfm_sprt
        psa_crypto_config
)

############################ Secure API ########################################

target_sources(tfm_sprt
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}/interface/src/rse_image_verification_api.c
)

target_include_directories(tfm_sprt
    PUBLIC
        .
        ${CMAKE_CURRENT_SOURCE_DIR}/interface/include
)

############################ Partition Defs ####################################

target_link_libraries(tfm_partitions
    INTERFACE
        tfm_app_rot_partition_rse_image_verification
)

target_compile_definitions(tfm_config
    INTERFACE
        TFM_PARTITION_RSE_IMAGE_VERIFICATION
)


install(FILES       ${CMAKE_CURRENT_SOURCE_DIR}/interface/src/rse_image_verification_api.c
        DESTINATION ${INSTALL_INTERFACE_SRC_DIR})

install(FILES       ${CMAKE_CURRENT_SOURCE_DIR}/interface/include/rse_image_verification_api.h
                    ${CMAKE_CURRENT_SOURCE_DIR}/interface/include/rse_image_verification_defs.h
        DESTINATION ${INSTALL_INTERFACE_INC_DIR})
